#include "form/core/accessibility_interface.nvgt"
#include "form/core/control.nvgt"
#include "form/events/event.nvgt"
#include "speech.nvgt"

class AccessibilityManager {
    private bool interrupt_on_focus = true;
    
    void announce_control(IControl@ control, bool interrupt = true) {
        if (@control is null) return;
        
        string announcement = build_announcement(control);
        if (announcement.length() > 0) {
            speak(announcement, interrupt);
        }
    }
    
    string build_announcement(IControl@ control) {
        if (@control is null) return "";
        
        string name = control.get_accessible_name();
        string state = control.get_accessible_state();
        AccessKitRole role = control.get_accessible_role();
        string role_name = get_role_name(role);
        
        // Add unavailable state if disabled
        if (!control.is_enabled()) {
            if (state.length() > 0) state += " ";
            state += "unavailable";
        }
        
        if (role == ACCESS_KIT_LIST || role == ACCESS_KIT_SLIDER || role == ACCESS_KIT_RADIO_GROUP) {
            string announcement = name;
            if (announcement.length() > 0) announcement += " ";
            announcement += role_name;
            if (state.length() > 0) {
                announcement += " ";
                announcement += state;
            }
            return announcement;
        }
        
        // Default order: name + state + role
        string announcement = name;
        if (state.length() > 0) {
            if (announcement.length() > 0) announcement += " ";
            announcement += state;
        }
        if (role_name.length() > 0) {
            if (announcement.length() > 0) announcement += " ";
            announcement += role_name;
        }
        
        return announcement;
    }
    
    void handle_focus_changed(IControl@ control) {
        announce_control(control, interrupt_on_focus);
    }
    
    void handle_activated_event(Event@ event) {
        IControl@ control = event.source;
        if (@control is null) return;
        
        AccessKitRole role = control.get_accessible_role();
        string role_name = get_role_name(role);
        
        if (role == ACCESS_KIT_CHECK_BOX) {
            string state = control.get_accessible_state();
            speak(state, true);
        }
        else if (role == ACCESS_KIT_RADIO_GROUP) {
            string state = control.get_accessible_state();
            if (state != "no selection") {
                speak(state, true);
            }
        }
        else if (role == ACCESS_KIT_SLIDER) {
            string state = control.get_accessible_state();
            speak(state, true);
        }
    }
    
    void handle_event(Event@ event) {
        if (event.type == EventType::FocusGained) {
            handle_focus_changed(event.source);
        } else if (event.type == EventType::Activated) {
            handle_activated_event(event);
        }
    }
}
